#!ipxe

prompt --key 0x02 --timeout 3000 Press Ctrl-B for the iPXE command line... && shell ||

isset ${net0/mac} && ifopen net0 && dhcp net0 || goto dhcpnet1
echo Received DHCP answer on interface net0 && goto proxycheck

:dhcpnet1
isset ${net1/mac} && ifopen net1 && dhcp net1 || goto dhcpnet2
echo Received DHCP answer on interface net1 && goto proxycheck

:dhcpnet2
isset ${net2/mac} && ifopen net2 && dhcp net2 || goto dhcpall
echo Received DHCP anser on infterface net2 && goto proxycheck

:dhcpall
dhcp && goto proxycheck || goto dhcperror

:dhcperror
prompt --key s --timeout 10000 DHCP failed, hit 's' for the iPXE shell; reboot in 10 seconds && shell || reboot

:proxycheck
isset ${proxydhcp/next-server} && set next-server ${proxydhcp/next-server} || goto nextservercheck

:nextservercheck
isset ${next-server} && goto setdefaultnetboot || goto setserv

:setserv
echo -n Please enter tftp server: && read next-server && goto setdefaultnetboot || goto setserv

:setdefaultnetboot
initrd tftp://${next-server}/default.ipxe && set default-url tftp://${next-server} && goto netboot ||
initrd tftp://${next-server}/PXE/Grub/default.ipxe && set default-url tftp://${next-server}/PXE/Grub && goto netboot ||
prompt --key s --timeout 10000 Initrdloading failed, hit 's' for the iPXE shell; reboot in 10 seconds && shell || reboot

:netboot
imgfree default.ipxe
chain --autofree ${default-url}/default.ipxe && goto end || goto netbooterror

:netbooterror
prompt --key s --timeout 10000 Chainloading failed, hit 's' for the iPXE shell; reboot in 10 seconds && shell || reboot

:end